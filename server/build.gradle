plugins {
    id 'application'
}

group 'com.mythicalcreaturesoftware'
version '1.0.1'

application {
    mainClassName = 'com.mythicalcreaturesoftware.pcstatsmonitorserver.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.profesorfalken', name: 'jSensors', version: '2.2.1'
    compile group: 'org.fusesource', name: 'sigar', version: '1.6.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task copyLibs(type: Copy) {
    from("${sourceSets.main.output.resourcesDir}/libs")
    into("${projectDir}/scripts/libs")
    include '*.*'
    include 'libs/**'
}

task copyJar(type: Copy) {
    from "${project.buildDir}/libs"
    into "${projectDir}/scripts"
    rename { String fileName ->
        fileName.replace("-${version}", "")
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Implementation-Title': 'PC Stats Monitor Server',
                'Implementation-Version': version,
                'Main-Class': 'com.mythicalcreaturesoftware.pcstatsmonitorserver.Main'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task zipJar(type: Zip) {
    from "${projectDir}/scripts"
    include '*'
    include '*/*'
    archiveName 'server.zip'
    destinationDir(file("${projectDir}/release"))
}

task tarJar(type: Tar) {
    from "${projectDir}/scripts"
    include '*'
    include '*/*'
    archiveName = 'server.tar.gz'
    destinationDir  file("${projectDir}/release")
    compression = Compression.GZIP
}

task release (dependsOn: ['clean', 'fatJar', 'copyLibs', 'copyJar', 'zipJar', 'tarJar'])
fatJar.mustRunAfter build
copyJar.mustRunAfter fatJar
tarJar.mustRunAfter copyJar
zipJar.mustRunAfter copyJar